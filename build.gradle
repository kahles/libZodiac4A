buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'com.gladed.androidgitversion' version '0.4.9'
    // TODO needed? move to root? --> id 'io.codearte.nexus-staging' version '0.21.0'
}

apply plugin: 'com.android.library'
apply plugin: 'com.bmuschko.nexus'

group = "de.kah2.zodiac"
version = androidGitVersion.name() + "-SNAPSHOT"

repositories {
    jcenter()
    google()
    mavenCentral()
    /* For snapshots:
    maven {
        url =  'https://oss.sonatype.org/content/repositories/snapshots/'
    } */
}

dependencies {

    implementation 'org.slf4j:slf4j-api:1.7.26'

    // Removed, because actual version doesn't work for multi-threaded use
    // compile 'net.sourceforge.novaforjava:novaforjava:0.15.0.0'

    // my fixed fork: https://github.com/kahles/nova4jmt

    // local use
    // compile files('../nova4java/target/nova4jmt-0.15.0.1-SNAPSHOT.jar')

    // maven central snapshot (uncomment also above in repositories)
    //compile 'de.kah2.zodiac:nova4jmt:0.15.0.1-SNAPSHOT'

    // maven central releases
    implementation 'de.kah2.zodiac:nova4jmt:0.15.0.1'

    implementation 'com.android.support:appcompat-v7:27.1.1'

    // Android backports
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'
    implementation 'net.sourceforge.streamsupport:android-retrofuture:1.6.3'

    // Testing
    testImplementation 'junit:junit:4.12'
    testImplementation('org.threeten:threetenbp:1.4.0') {
        exclude group: 'com.jakewharton.threetenabp', module: 'threetenabp'
    }
    testImplementation 'org.slf4j:slf4j-simple:1.7.26'

    /*
    Not needed at the moment
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
     */
}

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27

        versionCode androidGitVersion.code()
        versionName androidGitVersion.name()

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

modifyPom {
    project {
        name = 'libZodiac4A'
        description = 'android framework providing functionality for building a lunar calendar'
        url = 'https://github.com/kahles/libZodiac4A'

        scm {
            url = 'https://github.com/kahles/libZodiac4A/tree/master'
            connection = 'scm:git:git://github.com/kahles/libZodiac4A.git'
            developerConnection = 'scm:git:ssh://github.com/kahles/libZodiac4A.git'
        }

        licenses {
            license {
                name = 'GNU LESSER GENERAL PUBLIC LICENSE (LGPL) version 3'
                url = 'http://www.gnu.org/licenses/lgpl-3.0-standalone.html'
            }
        }

        developers {
            developer {
                id = 'kahles'
                name = 'Stephan Kahle'
                email = 'st-mvn@kah2.de'
                timezone = '+1'
            }
        }
    }
}