buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
    id 'signing'
    id 'com.gladed.androidgitversion' version '0.4.14'
}

androidGitVersion {
    format '%tag%%-count%%-commit%%-branch%%_dirty%'
    hideBranches = ['master','devel']
}

println(" Version name:\t " + androidGitVersion.name() )
println(" Version code:\t " + androidGitVersion.code() )

group = 'de.kah2.zodiac'
version = androidGitVersion.name()

repositories {
    google()
    mavenLocal()
    mavenCentral()
}

dependencies {

    implementation 'org.slf4j:slf4j-api:2.0.17'
    testImplementation 'org.slf4j:slf4j-simple:2.0.17'
    
    // Removed, because actual version doesn't work for multi-threaded use
    // compile 'net.sourceforge.novaforjava:novaforjava:0.15.0.0'

    // my fixed fork: https://github.com/kahles/nova4jmt

    // local use
    // compile files('../nova4java/target/nova4jmt-0.15.0.1-SNAPSHOT.jar')

    // maven central snapshot (uncomment also above in repositories)
    //compile 'de.kah2.zodiac:nova4jmt:0.15.0.1-SNAPSHOT'

    // maven central releases
    implementation 'de.kah2.zodiac:nova4jmt:0.15.0.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.assertj:assertj-core:3.25.3'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        release(MavenPublication) {

            groupId = group
            artifactId = 'libZodiac'
            version = androidGitVersion.name()

            from components.java

            pom {
                name = 'libZodiac'
                description = 'android framework providing functionality for building a lunar calendar'
                url = 'https://github.com/kahles/libZodiac'

                licenses {
                    license {
                        name = 'GNU LESSER GENERAL PUBLIC LICENSE (LGPL) version 3'
                        url = 'http://www.gnu.org/licenses/lgpl-3.0-standalone.html'
                    }
                }

                developers {
                    developer {
                        id = 'kahles'
                        name = 'Stephan Kahle'
                        email = 'st-mvn@kah2.de'
                    }
                }

                scm {
                    url = 'https://github.com/kahles/libZodiac4A/tree/master'
                    connection = 'scm:git:git://github.com/kahles/libZodiac4A.git'
                    developerConnection = 'scm:git:ssh://github.com/kahles/libZodiac4A.git'
                }
            }
        }
    }
}

if ( !project.hasProperty( "sonatypeUsername" ) ) {
    ext.sonatypeUsername = "none"
}
if ( !project.hasProperty( "sonatypePassword" ) ) {
    ext.sonatypePassword = "none"
}

nexusPublishing {
    repositories {
        sonatype {
            username = sonatypeUsername
            password = sonatypePassword
        }
    }
}

signing {
    sign publishing.publications.release
}
