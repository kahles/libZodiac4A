buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.2'
    }
}

plugins {
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
    id 'signing'
    id 'com.gladed.androidgitversion' version '0.4.14'
}

apply plugin: 'com.android.library'

androidGitVersion {
    format '%tag%%-count%%-commit%%-branch%%_dirty%'
    hideBranches = ['master','devel']
}

afterEvaluate {
    println(" Version name:\t " + androidGitVersion.name() )
    println(" Version code:\t " + androidGitVersion.code() )
}

group = 'de.kah2.zodiac'
version = androidGitVersion.name()

repositories {
    google()
    mavenLocal()
    mavenCentral()
}

dependencies {

    implementation 'org.slf4j:slf4j-api:2.0.17'
    testImplementation 'org.slf4j:slf4j-simple:2.0.17'
    
    // Removed, because actual version doesn't work for multi-threaded use
    // compile 'net.sourceforge.novaforjava:novaforjava:0.15.0.0'

    // => my fixed fork: https://github.com/kahles/nova4jmt

    // maven central snapshot (uncomment also above in repositories)
    //api 'de.kah2.zodiac:nova4jmt:0.15.0.1-SNAPSHOT'

    // maven central releases
    api 'de.kah2.zodiac:nova4jmt:0.15.0.1'

    //implementation 'com.android.support:appcompat-v7:27.1.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.1'
    testImplementation 'org.assertj:assertj-core:3.27.3'
}

android {

    namespace 'de.kah2.zodiac'

    compileSdk 34

    defaultConfig {

        minSdkVersion 26
        targetSdkVersion 34

        versionCode androidGitVersion.code()
        versionName androidGitVersion.name()

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {

            groupId = 'de.kah2.zodiac'
            artifactId = 'libZodiac4A'
            version = androidGitVersion.name()

            afterEvaluate {
                from components.release
            }

            pom {
                name = 'libZodiac4A'
                description = 'android framework providing functionality for building a lunar calendar'
                url = 'https://github.com/kahles/libZodiac4A'

                licenses {
                    license {
                        name = 'GNU LESSER GENERAL PUBLIC LICENSE (LGPL) version 3'
                        url = 'http://www.gnu.org/licenses/lgpl-3.0-standalone.html'
                    }
                }

                developers {
                    developer {
                        id = 'kahles'
                        name = 'Stephan Kahle'
                        email = 'st-mvn@kah2.de'
                    }
                }

                scm {
                    url = 'https://github.com/kahles/libZodiac4A/tree/master'
                    connection = 'scm:git:git://github.com/kahles/libZodiac4A.git'
                    developerConnection = 'scm:git:ssh://github.com/kahles/libZodiac4A.git'
                }
            }
        }
    }
}

if ( !project.hasProperty( "sonatypeUsername" ) ) {
    ext.sonatypeUsername = "none"
}
if ( !project.hasProperty( "sonatypePassword" ) ) {
    ext.sonatypePassword = "none"
}

nexusPublishing {
    repositories {
        sonatype {
            username = sonatypeUsername
            password = sonatypePassword
        }
    }
    repositories {
    }
}

signing {
    sign publishing.publications.release
}
